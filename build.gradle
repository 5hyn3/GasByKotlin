plugins {
    id 'org.jetbrains.kotlin.js' version '1.4.0'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    testImplementation "org.jetbrains.kotlin:kotlin-test-js"
    implementation npm("@types/google-apps-script", "1.0.16", false) // 今の所なぜかコンパイルエラーになってしまうのでgenerateExternalsをfalseに設定
    implementation npm("@google/clasp", "2.3.0")
    implementation npm("browserify", "16.5.2")
    implementation npm("gasify", "1.0.1")
}

kotlin {
    js {
        browser {
            webpackTask {
                output.libraryTarget = "commonjs"
            }

            testTask {
                useKarma {
                    useChromeHeadless()
                    webpackConfig.cssSupport.enabled = true
                }
            }
        }
        binaries.executable()
    }
}

task convertForGas(type: Exec) {
    executable "$buildDir/js/node_modules/.bin/browserify"
    args "./gas/main.js", "-p", "$buildDir/js/node_modules/gasify", "-o", "./gas_dist/Code.js"
}

task push(type: Exec) {
    executable "$buildDir/js/node_modules/.bin/clasp"
    args "push"
}

task buildAndPush {
    dependsOn build
    dependsOn convertForGas
    dependsOn push
}